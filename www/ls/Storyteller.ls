stories =
  * title: "Česko se bojí terorismu"
    topHeading: null
    groupName: "uvodka"
    pages: [text: "<p>Zatím nikdy nebylo v Evropě tak bezpečně jako v posledních letech. Ještě na začátku devadesátých let měly teroristické útoky na evropském území kolem dvou set obětí ročně, loni to bylo sedm lidí.</p>"]
  * title: "Severní Irsko: Dvacet let odvetných výbuchů"
    topHeading: "Irský separatismus"
    groupName: "Konflikt v Severním Irsku"
    pages:
      * text: "<p>Severoirský konflikt byl ve sledovaném období nejkrvavější, stál přes 3 tisíce životů. Nejvyhrocenější byl v sedmdesátých letech, kdy po většině incidentů následovala pomsta druhé strany, která obvykle zasáhla civilisty. Hlavním způsobem boje byly bomby umístěné v autech nebo nastražené u cesty. V roce 1973 se útoky rozšířily ze Severního Irska na kontinentální Británii a „profesionalizovaly“ – provádění bombových útoků i komunikace s policií dostala pravidla, která měla snížit počet mrtvých. Každá nastražená bomba měla být oznáma předem a IRA dostala kód, který měla při telefonátu na policii použít. Policie díky němu dokázala rozlišit, kdy je hrozba bombou vážně míněná. V roce 1974 v Anglii vybuchla bomba průměrně každé tři dny. </p>"
      * text: "<p>Po uzavření příměří v roce 1998 pokračovaly a pokračují v ozbrojeném boji radikální sekce IRA (Skutečná IRA, Pokračující IRA, dnes Nová IRA), ale britské politické ústupky a veřejné mínění už násilnému boji nepřejí.</p>
<p>Dvě třetiny obětí konfliktu má na svědomí IRA a další separatistické organizace, asi tisícovku unionisti, zejména z UVF.</p>"
  * title: "Baskicko: proti ETA vláda stvořila vlastní teroristy"
    topHeading: "Baskický separatismus"
    groupName: "Baskický separatismus"
    pages:
      * text: "<p>Od devadesátých let slábne i baskická separatistická skupina ETA, strůjce druhého největšího evropského konfliktu s víc než osmi stovkami obětí. Nejaktivnější byla na konci sedmdesátých a po většinu osmdesátých let, vůbec nejkrvavější byl rok 1980 s 94 mrtvými. V druhé polovině devadesátých let vyhlásila sérii příměří, které ovšem pokaždé sama porušila. Zatím poslední, dosud platné a zřejmě definitivní příměří bylo dohodnuto v roce 2011. </p>"
      * text: "<p>Během divokého přelomu sedmdesátých a osmdesátých let španělská vláda baskickým separatistům stvořila mimo rámec zákona protivníka, který používal podobné metody, včetně bombových útoků: nejprve ultrapravicový Španělský baskický batalion, po něm Protiteroristickou osvobozeneckou skupinu. Dohromady měly na svědomí kolem třiceti vražd členů ETA. </p>"
  * title: "Islámský radikalismus: méně časté, ale dobře viditelné útoky"
    topHeading: "Islámský terorismus"
    groupName: "Arabský a islámský terorismus"
    pages:
      * text: "<p>Nejkrvavější evropské útoky mají na svědomí skupiny, spojené nejprve bojem proti Izraeli a Spojeným státům, později radikálním výkladem islámu. Do devadesátých let arabskému terorismu dominovaly skupiny jako Organizace pro osvobození Palestiny, Abú Nidal, Černé září nebo Hizballáh, často podporované libyjským diktátorem Muammarem Kaddáfím. Typickým způsobem jejich práce byly únosy rukojmí, často v letadlech, a následné požadavky na propuštění vězněných Palestinců. K nejznámnějším incidentům patří únos letu 139 společnosti Air France z Tel Avivu do Paříže, který skončil úspěšným zásahem izraelských ozbrojených sil, nebo přepadení izraelského olympijského týmu v Mnichově 1972.</p>"
      * text: "<p>Po útocích z 11. září se zvýšila bezpečnostní opatření způsob a následně i způsob boje: začali útočit jednotlivci, inspirovaní al-Kaidou. Na rozdíl od organizací, spojených s Palestinou, většinou nemají žádné požadavky. Místo boje proti Izraeli je pojí radikální výklad islámu. </p>
<p>Arabští nacionalisti a později islamisti mají sice na svědomí největší evropské útoky, ale uspějí poměrně vzácně. Mají tedy větší mediální pozornost, než skutečné výsledky.</p>
<p>Od počátku devadesátých let uspěly v Evropě pouze dvě velké teroristické akce: vlakové útoky v Madridu v roce 2004 a útok na londýnské metro o rok později. Dohromady si vyžádaly 250 životů.</p>"
  * title: "Extrémní <span class='extreme-right'>pravice</span> a <span class='extreme-left'>levice</span>: společný vrchol zažily v&nbsp;80.&nbsp;letech"
    topHeading: "Politický extrémismus"
    groupName: "Politický extrémismus"
    pages:
      * text: "<p>Extrémní <span class='extreme-right'>pravice</span> a <span class='extreme-left'>levice</span> mají od sedmdesátých let shodně na svědomí přibližně 250 mrtvých. Oba směry byly nejviditelnější na přelomu sedmdesátých a osmdesátých let. Extrémní <span class='extreme-right'>pravice</span> ohrožovala hlavně Itálii, neofašistická skupina Nuclei Armati Rivoluzionari měla ke stovce obětí. Extrémní <span class='extreme-left'>levice</span> působila ve více jihoevropských zemích: mezi nejsilnějšími byly Rudé brigády v Itálii (65 mrtvých), Antifašistická odbojová skupina 1. října ve Španělsku (84 mrtvých) nebo Řecku Revoluční organizace 17. listopadu v Řecku (26 mrtvých).</p>"
      * text: "<p>Od devadesátých let jsou násilnosti, spojené s oběma ideologiemi, na ústupu. Výjimkou je útok norského neonacisty Anderse Breivika v roce 2011.</p>"
  * title: "Příležitostní teroristi: Italská mafie, Arméni nebo bojovníci za Jižní Moluky"
    topHeading: "Menší uskupení"
    groupName: "Ostatní"
    pages:
      * text: "<p>V osmdesátých a devadesátých letech se mezi nejviditelnější teroristické skupiny dostala italská mafie. Nejprve umístila bombu ve vlaku z Neapole do Milána, aby odvrátila pozornost od monstrprocesu, zaměřenému právě proti členům mafie. Když se vyšetřovateli Falconemu přesto podařilo dostat před soud téměř pět stovek lidí, nechala obviněná mafie zlikvidovat nejprve jeho, o pár měsíců později jeho kolegu Borsallineho.</p>"
      * text: "<p>Na zlomu sedmdesátých a osmdesátých let bylo v Evropě aktivních i několik hnutí, která požadovala oficiální uznání tureckého vraždění Arménů během první světové války za genocidu. Bojovali za ně, jak jinak, vražděním Turků. Bomby obvykle umisťovali k přepážkám tureckých leteckých společností nebo před turecká velvyslanectví ve velkých evropských městech.</p>"
      * text: "<p>Teroristické útoky v polovině sedmdesátých let zasáhly i jinak klidné Nizozemsko. V jeho armádě za druhé světové války bojovalo kolem 12 tisíc mužů z Jižní Moluky, části indonéských ostrovů. Po válce byli nedobrovolně přesunuti do Nizozemska, kde přežívali v dočasných utečeneckých táborech a čekali, až nizozemská vláda splní svůj slib vytvořit nezávislý stát. To se nikdy nedostalo. Po 25 letech v dočasných táborech indonéské menšině došla trpělivost a dvakrát unesli vlak i s několika desítkami rukojmí. Oba pokusy byly neúspěšné a Jižní Moluky nevznikly. Další generace se v nizozemské společnosti úspěšně integrovaly.</p>"
  * title: "Česko: převažují rasistické útoky"
    topHeading: "Terorismus v Česku"
    groupName: "Terorismus v Česku"
    pages:
      * text: "<p>Na československém a později českém území se podle databáze odehrálo přes třicet teroristických útoků, při sedmi z nich oběť zemřela. Mezi nimi vyčnívá bombový útok na let 367 jugoslávských aerolinek ze Stockholmu do Bělehradu z ledna 1972. Letadlo se po explozi zřítilo na československé území nedaleko Srbské Kamenice. Z 28 lidí na palubě se zázračně zachránila jen dvacetiletá letuška Vesna Vulović. Pád z 10 160 metrů s těžkými zraněními přežila - je tak jediným člověkem, který přežil pád z podobné výšky. Švédským novinám se později ozval Chorvat, který se přihlásil k útoku. Vyšetřování nehody ale k žádné konkrétní skupině nevedlo.</p>"
      * text: "<p>Další tragický incident nastal už v samostatné České republice v listopadu 1996. Při výbuchu vojenského granátu v ostravské nemocnici na Fifejdách zahynuly dvě ženy; podle ministerstva vnitra ovšem o teroristický útok nešlo. „Granát pravděpodobně našla jedna z žen na ulici a přinesla ho do nemocnice,“ uvádí na svých stránkách. </p>"
      * text: "<p>Ani další útoky na českém území nejsou typickými teroristickými útoky. Ve dvou případech šlo o rasistické útoky proti Romům, jednou proti súdánskému studentovi. Dalším případem je sebevražedná exploze Rakušana v příhraničních Českých Velenicích; došlo k ní nicméně na odlehlém místě, nikdo jiný nebyl zraněn a žádná skupina se k akci nepřihlásila. Posledním případem je dopisová bomba, která zabíjela v Praze, ke které se rovněž žádná skupina nepřihlásila.</p>"
      * text: "<p>Nejnovějšími incidenty jsou dvě anonymní dopisové bomby z listopadu 2014, které dorazily ministru vnitra Chovancovi a ministru financí Babišovi. Nikdo nebyl zraněn a nikdo se k útoku nepřihlásil. Podle databáze nicméně útok může souviset s českou podporou boje proti Islámskému státu v Iráku a Levantu.</p>"



class ig.Storyteller
  (@parentElement) ->
    ig.Events @
    @currentStory = 0
    @currentPage = 0
    @element = @parentElement.append \div
      ..attr \class \story
    @contentElement = @element.append \div
      ..attr \class \content
    @titleElement = @contentElement.append \h2
      ..html stories[@currentStory].title
    @paragraphElement = @contentElement.append \div
      ..attr \class \paragraph
    @paragraphInner = @paragraphElement.append \div
      ..attr \class \container
      ..html stories[@currentStory].pages[@currentPage].text
    @createPager!
    @createStorySelector!

  createPager: ->
    @pager = @element.append \div
      ..attr \class "pager display-next display-hint"
      ..append \a
        ..attr \class \prev
        ..attr \href \#
        ..html "<span class='arrow'>‹</span>"
        ..on \click ~>
          d3.event.preventDefault!
          @changePage -1
        ..on \mousedown ~> d3.event.preventDefault!
      ..append \a
        ..attr \class \next
        ..attr \href \#
        ..html "<span class='hint'>Číst dál</span><span class='arrow'>›</span>"
        ..on \click ~>
          d3.event.preventDefault!
          @changePage +1
        ..on \mousedown ~> d3.event.preventDefault!

  changePage: (direction, changeStory = no) ->
    @currentPage += direction
    if @currentPage > stories[@currentStory].pages.length - 1
      @currentStory++
      changeStory = yes
      @currentPage = 0
    if @currentPage < 0
      @currentStory--
      changeStory = yes
      @currentPage = 0
    @pager.classed \display-next not (@isLastStory! && @isLastPage!)
    @pager.classed \display-prev not (@currentStory == @currentPage == 0)
    @pager.classed \display-hint no

    if changeStory
      @changeStory!
      @emit \story stories[@currentStory].groupName
    else
      @changeParagraph!
    @updateStorySelector!

  changeStory: ->
    @contentElement
      ..classed \leaving yes
      ..transition!
        ..duration 800
        ..remove!
    @contentElement = @element.append \div
      ..attr \class "content entering"
      ..transition!
        ..delay 1
        ..attr \class \content
    @titleElement = @contentElement.append \h2
      ..html stories[@currentStory].title
    @paragraphElement = @contentElement.append \div
      ..attr \class \paragraph
    @paragraphInner = @paragraphElement.append \div
      ..attr \class \container
      ..html stories[@currentStory].pages[@currentPage].text


  changeParagraph: ->
    @paragraphInner
      ..classed \leaving yes
      ..transition!
        ..duration 800
        ..remove!
    @paragraphInner = @paragraphElement.append \div
      ..attr \class "container entering"
      ..html stories[@currentStory].pages[@currentPage].text
      ..transition!
        ..delay 1
        ..attr \class "container"

  showIncident: (incident) ->
    @contentElement.classed \leaving yes
    @tempContent = @element.append \div
      ..attr \class "content temp-content"
      ..transition!
        ..delay 1
        ..attr \class "content temp-content"
      ..append \p .html incident.text

  hideIncident: ->
    @tempContent
      ..classed \leaving yes
      ..transition!
        ..duration 800
        ..remove!
    @contentElement.classed \leaving no

  createStorySelector: ->
    selectableStories = stories.slice 1
    @storySelectorElements = @parentElement.append \ul
      .attr \class \story-selector
      .selectAll \li .data selectableStories .enter!append \li
        .append \a
          ..attr \href \#
          ..html (.topHeading)
          ..on \click (d, i) ~>
            d3.event.preventDefault!
            i += 1
            return if i == @currentStory
            @currentStory = i
            @currentPage = 0
            @changePage 0, yes


  updateStorySelector: ->
    @storySelectorElements.classed \active (d, i) ~>
      @currentStory == i + 1

  isLastStory: ->
    @currentStory == stories.length - 1

  isLastPage: ->
    @currentPage == stories[@currentStory].pages.length - 1
